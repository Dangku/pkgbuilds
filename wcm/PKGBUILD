# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname=wcm
pkgver=0.7.0.r6.g33772f0
pkgrel=1
pkgdesc='Wayfire Configuration Manager'
url=https://wayfire.org
arch=(x86_64 aarch64)
license=(custom:MIT)
provides=("wdisplays")
conflicts=("wdisplays")
depends=(gtkmm3 wayfire wf-shell)
makedepends=(wayland-protocols meson ninja git extra-cmake-modules)
optdepends=("wayfire-plugins-extra: configuration for extra Wayfire plugins")
_commit=33772f07eec298b65b41fbb0d412c7d06c403e33
source=("${pkgname}::git+https://github.com/WayfireWM/wcm#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  ( set -o pipefail
    git describe --long --tag 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
  )
}

build () {
  rm -rf build
  arch-meson "${pkgname}" build
  ninja -C build
}

package () {
  DESTDIR="${pkgdir}" ninja -C build install
}

