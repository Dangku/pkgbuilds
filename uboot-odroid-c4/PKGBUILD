# U-Boot: ODROID-C4/HC4
#
# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

buildarch=8

pkgname=uboot-odroid-c4
_revision=158
pkgver=2015.01.${_revision}
pkgrel=2
pkgdesc="U-Boot for ODROID-C4/HC4"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
depends=('uboot-tools')
conflicts=('uboot-odroid-n2')
makedepends=('git' 'bc')
source=(
  "https://github.com/hardkernel/u-boot/releases/download/travis/odroidn2-${_revision}/u-boot-odroidc4-${_revision}.tar.gz"
  'sd_fusing.sh'
  '91-uboot-uimg.hook'
  '92-uboot-bootini.hook'
  'boot.mainline.ini'
  'boot.hardkernel.ini'
  'odroid-bootini'
)
md5sums=(
  '10eeb8d73ab78b3d2334408d56c6efab'
  '3bd43e59c5f88a7544505288627fe2dd'
  '1931c8dfde7088530f173ca59fdb8989'
  'e1a78b20ea7fe68ed3bb6fe24ff54110'
  '22b7a204878962eb2bdc9b509ca5d871'
  '0c436689f5a6c85a2f569322d182a928'
  '23ecb7e8e09507db7a1bddaa162c1507'
)

package() {
  cd sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}/sd_fusing.sh" "${pkgdir}"/boot

  install -Dm644 "${srcdir}/91-uboot-uimg.hook" "${pkgdir}/usr/share/libalpm/hooks/91-uboot-uimg.hook"
  install -Dm644 "${srcdir}/92-uboot-bootini.hook" "${pkgdir}/usr/share/libalpm/hooks/92-uboot-bootini.hook"

  install -Dm644 "${srcdir}/boot.hardkernel.ini" "${pkgdir}/usr/share/${pkgname}/boot.hardkernel.ini"
  install -Dm644 "${srcdir}/boot.mainline.ini" "${pkgdir}/usr/share/${pkgname}/boot.mainline.ini"

  install -Dm755 "${srcdir}/odroid-bootini" "${pkgdir}/usr/bin/odroid-bootini"
}
