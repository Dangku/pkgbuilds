# U-Boot: BANANAPI M5/M2PRO
#
# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

buildarch=8

pkgname=uboot-bananapi-m5
_revision=001
pkgver=2015.01.${_revision}
pkgrel=1
_edid_generator_commit=31a6f80784d289d2faa8c4ca4788409c83b3ea14
pkgdesc="U-Boot for BANANAPI M5/M2PRO"
arch=('aarch64')
url="https://github.com/Dangku/amlogic-u-boot/tree/odroidg12-v2015.01-c4-m5"
license=('GPL')
install=$pkgname.install
depends=('uboot-tools' 'util-linux' 'gawk' 'sed')
conflicts=('uboot-odroid-n2')
source=(
  "https://github.com/Dangku/amlogic-u-boot/releases/download/dangku/bananapi-m5-${_revision}/u-boot-bananapi-m5-${_revision}.tar.gz"
  "https://github.com/akatrevorjay/edid-generator/archive/${_edid_generator_commit}.tar.gz"
  "edid.md"
  'sd_fusing.sh'
  '91-uboot-uimg.hook'
  '92-uboot-bootini.hook'
  'boot.mainline.ini'
  'boot.bananapi.ini'
  'bananapi-bootini'
)
md5sums=(
  '0e1aee751e49caed9726d81bb76d537f'
  '4be5fae86f996be8540a4344a8b8c120'
  '1e821a54a437e11876940e8446819ecf'
  '3bd43e59c5f88a7544505288627fe2dd'
  '1931c8dfde7088530f173ca59fdb8989'
  '7edc985e2fa767f5ecbf989c24d85ee6'
  '06f54b405a6f31d6f179ed0e011f4f2e'
  '860d197e406b7d9187e24c6d91d98212'
  '226d6187e9e8e24e27d1375eec08c980'
)

package() {
  cd sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}/sd_fusing.sh" "${pkgdir}"/boot

  install -Dm644 "${srcdir}/91-uboot-uimg.hook" "${pkgdir}/usr/share/libalpm/hooks/91-uboot-uimg.hook"
  install -Dm644 "${srcdir}/92-uboot-bootini.hook" "${pkgdir}/usr/share/libalpm/hooks/92-uboot-bootini.hook"

  install -Dm644 "${srcdir}/boot.bananapi.ini" "${pkgdir}/usr/share/${pkgname}/boot.bananapi.ini"
  install -Dm644 "${srcdir}/boot.mainline.ini" "${pkgdir}/usr/share/${pkgname}/boot.mainline.ini"

  install -Dm755 "${srcdir}/bananapi-bootini" "${pkgdir}/usr/bin/bananapi-bootini"

  mkdir -p "${pkgdir}"/usr/lib/firmware/edid
  cp "${srcdir}/edid-generator-${_edid_generator_commit}/"*.bin "${pkgdir}"/usr/lib/firmware/edid/
  cp "${srcdir}/edid.md" "${pkgdir}"/usr/lib/firmware/edid/README.md
}
