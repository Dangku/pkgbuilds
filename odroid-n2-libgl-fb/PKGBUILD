# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgname=odroid-n2-libgl-fb
pkgver=0.1
pkgrel=5
_pkgdate=202005130727
pkgdesc="The framebuffer userspace binary driver for Mali GPU G52."
arch=('aarch64')
url="https://developer.arm.com/products/software/mali-drivers/user-space"
license=('custom:Mali-GPU-Userspace-Driver')
depends=(
  'mesa'
  'xf86-video-fbdev'
  'dkms-mali-bifrost'
)
conflicts=(
  'mali-utgard-meson-libgl-fb'
  'mali-utgard-meson-libgl-x11'
  'odroid-c2-libgl-fb'
  'odroid-c2-libgl-x11'
  'mali-bifrost-wayland-driver'
  'mali-bifrost-fbdev-driver'
  'xf86-video-fbturbo-git'
)
options=('!strip')
source=(
  "mali.conf"
  "99-fbdev.conf"
  "http://ppa.linuxfactory.or.kr/pool/non-free/m/mali-bifrost-driver/mali-bifrost-fbdev-driver_${pkgver}-${pkgrel}+${_pkgdate}~focal_arm64.deb"
)
md5sums=(
  '2ac761dc95cf5096d9313ad993dcf4be'
  'ee384b23881fa9eef48924937382ea61'
  'bb65cb3da49fc89bb101fa2cad3d0b9d'
)

build() {
  cd ${srcdir}

  ar x mali-bifrost-fbdev-driver_${pkgver}-${pkgrel}+${_pkgdate}~focal_arm64.deb
  tar -xvJf data.tar.xz

  cd usr/lib/aarch64-linux-gnu

  ln -sf libEGL.so.1         libEGL.so
  ln -sf libEGL.so.1.1.0     libEGL.so.1
  ln -sf /usr/lib/libMali.so libEGL.so.1.1.0

  ln -sf libGLESv1_CM.so.1   libGLESv1_CM.so
  ln -sf /usr/lib/libMali.so libGLESv1_CM.so.1

  ln -sf libGLESv2.so.2      libGLESv2.so
  ln -sf libGLESv2.so.2.1.0  libGLESv2.so.2
  ln -sf /usr/lib/libMali.so libGLESv2.so.2.1.0

  ln -sf libOpenCL.so.1      libOpenCL.so
  ln -sf libOpenCL.so.1.0.0  libOpenCL.so.1
  ln -sf /usr/lib/libMali.so libOpenCL.so.1.0.0
}

package() {
  install -d $pkgdir/etc/ld.so.conf.d
  install -d $pkgdir/etc/X11/xorg.conf.d
  install -d $pkgdir/usr/lib/mali-egl
  install -d $pkgdir/usr/share/doc/odroid-n2-libgl-fb

  cp mali.conf $pkgdir/etc/ld.so.conf.d/
  cp 99-fbdev.conf $pkgdir/etc/X11/xorg.conf.d/

  cp $srcdir/usr/lib/aarch64-linux-gnu/mali-g52/fbdev/libmali.so $pkgdir/usr/lib/libMali.so

  rm -rf $srcdir/usr/lib/aarch64-linux-gnu/mali-g52
  rm -rf $srcdir/usr/lib/aarch64-linux-gnu/mali-g31

  cp -a usr/lib/aarch64-linux-gnu/* $pkgdir/usr/lib/mali-egl/

  cp usr/share/doc/mali-bifrost-fbdev-driver/copyright $pkgdir/usr/share/doc/odroid-n2-libgl-fb/
}