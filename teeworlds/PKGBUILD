# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Armin 'IsSuE' Praher [issue.at.gmx.dot.at]
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=teeworlds
pkgver=0.7.5
pkgrel=1
pkgdesc='Fast-paced multiplayer 2D shooter game'
arch=('x86_64' 'aarch64')
url='https://www.teeworlds.com'
license=('custom')
depends=('alsa-lib' 'glu' 'sdl2' 'freetype2' 'wavpack')
makedepends=('cmake' 'ninja' 'python' 'mesa' 'imagemagick' 'gendesk' 'git')
source=("git+git://github.com/teeworlds/teeworlds.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  convert +set date:create +set date:modify "${pkgname}/other/icons/teeworlds.ico" "${srcdir}/${pkgname}.png"
  gendesk -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" \
    --name 'Teeworlds' --categories 'Game;ArcadeGame'

  cd teeworlds
  git submodule update --init
}

build() {
  cd $srcdir/teeworlds

  cmake \
    -GNinja \
    -DCMAKE_BUILD_TYPE=release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    .

  ninja
}

package() {
  cd teeworlds

  DESTDIR="${pkgdir}" ninja install

  install -Dm644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
  install -Dm644 "${srcdir}"/${pkgname}-0.png "${pkgdir}"/usr/share/pixmaps/${pkgname}.png

  install -Dm644 license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
