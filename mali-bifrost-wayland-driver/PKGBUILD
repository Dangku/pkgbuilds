# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgname=mali-bifrost-wayland-driver
pkgver=r16p0
pkgrel=1
pkgdesc="The Wayland userspace binary driver for Mali GPU G31/G52."
arch=('aarch64')
url="https://dn.odroid.com/S922X/ODROID-N2/Wayland"
license=('custom:Mali-GPU-Userspace-Driver')
depends=('mesa')
provides=()
install=${pkgname}.install
conflicts=(
  "mali-utgard-meson-libgl-fb"
  "mali-utgard-meson-libgl-x11"
  "odroid-c2-libgl-fb"
  "odroid-c2-libgl-x11"
  "mali-bifrost-fbdev-driver"
)
options=('!strip')
source=(
  "mali.sh"
  "mali.conf"
  "http://ppa.linuxfactory.or.kr/pool/non-free/m/mali-bifrost-driver/mali-bifrost-wayland-driver_0.1-5+202004131528~focal_arm64.deb"
  "http://ppa.linuxfactory.or.kr/pool/main/libg/libgbm-compat/libgbm1_0.1.0+202004101002~focal_arm64.deb"
  "http://ppa.linuxfactory.or.kr/pool/main/libg/libgbm-compat/libgbm1-compat_0.1.0+202004141650~focal_arm64.deb"
)
md5sums=(
  '11c71896bcab2c59344b07c15722db0e'
  '2ac761dc95cf5096d9313ad993dcf4be'
  'e6b9685e5d638984b0250c3958cb74e8'
  '50346762ed55a9c38de263d5a981d513'
  '68e3b9eb283469d296fbe419f5e9bc34'
)

build() {
  cd ${srcdir}

  ar x libgbm1_0.1.0+202004101002~focal_arm64.deb
  tar -xvJf data.tar.xz

  ar x libgbm1-compat_0.1.0+202004141650~focal_arm64.deb
  tar -xvJf data.tar.xz

  ar x mali-bifrost-wayland-driver_0.1-5+202004131528~focal_arm64.deb
  tar -xvJf data.tar.xz

  cd usr/lib/aarch64-linux-gnu/mali-g52/wayland

  ln -sf libEGL.so.1       libEGL.so
  ln -sf libEGL.so.1.1.0   libEGL.so.1
  ln -sf libmali.so        libEGL.so.1.1.0

  ln -sf libGLESv1_CM.so.1 libGLESv1_CM.so
  ln -sf libmali.so        libGLESv1_CM.so.1

  ln -sf libGLESv2.so.2      libGLESv2.so
  ln -sf libGLESv2.so.2.1.0  libGLESv2.so.2
  ln -sf libmali.so          libGLESv2.so.2.1.0

  ln -sf libwayland-egl.so.1      libwayland-egl.so
  ln -sf libwayland-egl.so.1.0.0  libwayland-egl.so.1
  ln -sf libmali.so               libwayland-egl.so.1.0.0
}

package() {
  install -d $pkgdir/etc/profile.d
  install -d $pkgdir/etc/ld.so.conf.d
  install -d $pkgdir/usr/lib/mali-egl
  install -d $pkgdir/usr/share/doc/mali-bifrost-wayland-driver/

  cp mali.sh $pkgdir/etc/profile.d/
  cp mali.conf $pkgdir/etc/ld.so.conf.d/
  cp -a usr/lib/aarch64-linux-gnu/mali-g52/wayland/* $pkgdir/usr/lib/mali-egl/

  cp usr/lib/libgbm-compat.la $pkgdir/usr/lib/mali-egl/
  cp usr/lib/libgbm-compat.so $pkgdir/usr/lib/mali-egl/

  cp -a usr/lib/aarch64-linux-gnu/libgbm.la $pkgdir/usr/lib/mali-egl/
  cp -a usr/lib/aarch64-linux-gnu/libgbm.so $pkgdir/usr/lib/mali-egl/
  cp -a usr/lib/aarch64-linux-gnu/libgbm.so.1 $pkgdir/usr/lib/mali-egl/
  cp -a usr/lib/aarch64-linux-gnu/libgbm.so.1.0.0 $pkgdir/usr/lib/mali-egl/

  cp usr/share/doc/mali-bifrost-wayland-driver/copyright $pkgdir/usr/share/doc/mali-bifrost-wayland-driver/
}