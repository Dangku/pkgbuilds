# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgbase=odroid-c4-libgl
pkgname=("${pkgbase}-wl" "${pkgbase}-fb" "${pkgbase}-headers")
pkgver=1.0
pkgrel=2
_commit=5963003d4d86d9820a606f7301b3b76ac718b8fb
_srcdir=buildroot_linux_amlogic_meson_mali-${_commit}
arch=('aarch64')
url="https://github.com/hardkernel/buildroot_linux_amlogic_meson_mali"
license=('Proprietary')
options=('!strip')
conflicts=(
  "mali-utgard-meson-libgl-fb"
  "mali-utgard-meson-libgl-x11"
  "odroid-c2-libgl-fb"
  "odroid-c2-libgl-x11"
  "odroid-n2-libgl-fb"
  "odroid-n2-libgl-wl"
)
source=(
  "license"
  "https://github.com/hardkernel/buildroot_linux_amlogic_meson_mali/archive/${_commit}.zip"
  "odroid-mali-wl.sh"
  "odroid-mali-fb.sh"
  "mali-wl.pc"
  "mali-fb.pc"
)
md5sums=(
  '3da6d34563163f132815d9b8c5387a76'
  '270395c3de1c7b531ab20bf104a3a285'
  '4a82344afecef798cd6f66d38a5acf5a'
  '7278c961ccacede3e61b2f65a9215fc0'
  '485b088798399c496d1ba5e843986ae4'
  'd998e32a73dea315db09fe20cbf804fc'
)

prepare() {
  if [ -e "$srcdir/libs" ]; then
    rm -rf $srcdir/libs
  fi
}

package_odroid-c4-libgl-wl() {
  pkgdesc="ODROID-C4 Mali driver (Wayland)"
  install=$pkgname.install

  install -d $pkgdir/etc/ld.so.conf.d
  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/lib/mali-egl-wl
  install -d $pkgdir/usr/lib/pkgconfig
  install -d $pkgdir/usr/share/doc/$pkgname

  touch $pkgdir/etc/ld.so.conf.d/mali-wl.conf
  cp mali-wl.pc $pkgdir/usr/lib/pkgconfig/
  cp odroid-mali-wl.sh $pkgdir/usr/bin/odroid-mali-wl
  chmod 0755 $pkgdir/usr/bin/odroid-mali-wl

  cp $srcdir/$_srcdir/lib/arm64/dvalin/r16p0/wayland/drm/libMali.so \
    $pkgdir/usr/lib/libmali-wl.so

  chmod 0755 $pkgdir/usr/lib/libmali-wl.so

  ln -sf /usr/lib/mali-egl-wl/libmali.so \
    $pkgdir/usr/lib/mali-egl-wl/libmali.so.0

  cp license $pkgdir/usr/share/doc/$pkgname/

  ln -sf /usr/lib/libmali-wl.so    $pkgdir/usr/lib/mali-egl-wl/libmali.so

  ln -sf libEGL.so.1               $pkgdir/usr/lib/mali-egl-wl/libEGL.so
  ln -sf libEGL.so.1.1.0           $pkgdir/usr/lib/mali-egl-wl/libEGL.so.1
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-wl/libEGL.so.1.1.0

  ln -sf libGLESv1_CM.so.1         $pkgdir/usr/lib/mali-egl-wl/libGLESv1_CM.so
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-wl/libGLESv1_CM.so.1

  ln -sf libGLESv2.so.2            $pkgdir/usr/lib/mali-egl-wl/libGLESv2.so
  ln -sf libGLESv2.so.2.1.0        $pkgdir/usr/lib/mali-egl-wl/libGLESv2.so.2
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-wl/libGLESv2.so.2.1.0

  ln -sf libwayland-egl.so.1       $pkgdir/usr/lib/mali-egl-wl/libwayland-egl.so
  ln -sf libwayland-egl.so.1.0.0   $pkgdir/usr/lib/mali-egl-wl/libwayland-egl.so.1
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-wl/libwayland-egl.so.1.0.0
}

package_odroid-c4-libgl-fb() {
  pkgdesc="ODROID-C4 Mali driver (framebuffer)"
  depends=('mesa' 'xf86-video-fbdev')
  install=$pkgname.install

  install -d $pkgdir/etc/ld.so.conf.d
  install -d $pkgdir/etc/X11/xorg.conf.d
  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/lib/mali-egl-fb
  install -d $pkgdir/usr/lib/pkgconfig
  install -d $pkgdir/usr/share/doc/$pkgname

  touch $pkgdir/etc/ld.so.conf.d/mali-fb.conf
  touch $pkgdir/etc/X11/xorg.conf.d/60-fbdev.conf
  cp mali-fb.pc $pkgdir/usr/lib/pkgconfig/
  cp odroid-mali-fb.sh $pkgdir/usr/bin/odroid-mali-fb
  chmod 0755 $pkgdir/usr/bin/odroid-mali-fb

  cp $srcdir/$_srcdir/lib/arm64/dvalin/r12p0/fbdev/libMali.so \
    $pkgdir/usr/lib/libmali-fb.so

  chmod 0755 $pkgdir/usr/lib/libmali-fb.so

  cp license $pkgdir/usr/share/doc/$pkgname/

  ln -sf /usr/lib/libmali-fb.so    $pkgdir/usr/lib/mali-egl-fb/libmali.so

  ln -sf libEGL.so.1               $pkgdir/usr/lib/mali-egl-fb/libEGL.so
  ln -sf libEGL.so.1.1.0           $pkgdir/usr/lib/mali-egl-fb/libEGL.so.1
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-fb/libEGL.so.1.1.0

  ln -sf libGLESv1_CM.so.1         $pkgdir/usr/lib/mali-egl-fb/libGLESv1_CM.so
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-fb/libGLESv1_CM.so.1

  ln -sf libGLESv2.so.2            $pkgdir/usr/lib/mali-egl-fb/libGLESv2.so
  ln -sf libGLESv2.so.2.1.0        $pkgdir/usr/lib/mali-egl-fb/libGLESv2.so.2
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-fb/libGLESv2.so.2.1.0

  ln -sf libOpenCL.so.1            $pkgdir/usr/lib/mali-egl-fb/libOpenCL.so
  ln -sf libOpenCL.so.1.0.0        $pkgdir/usr/lib/mali-egl-fb/libOpenCL.so.1
  ln -sf libmali.so                $pkgdir/usr/lib/mali-egl-fb/libOpenCL.so.1.0.0
}

package_odroid-c4-libgl-headers() {
  pkgdesc="ODROID-C4 Mali driver headers"

  install -d $pkgdir/usr/include/mali

  cp -a $srcdir/$_srcdir/include/* $pkgdir/usr/include/mali/
}
