# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgname=odroid-c4-libgl-wl
pkgdesc="ODROID-C4 Mali driver (Wayland)"
pkgver=r16p0
pkgrel=1
arch=('aarch64')
url="https://github.com/hardkernel/buildroot_linux_amlogic_meson_mali"
license=('Proprietary')
depends=('dkms-mali-bifrost')
makedepends=('gcc')
conflicts=(
  "mali-utgard-meson-libgl-fb"
  "mali-utgard-meson-libgl-x11"
  "odroid-c2-libgl-fb"
  "odroid-c2-libgl-x11"
  "odroid-c4-libgl-fb"
  "odroid-n2-libgl-fb"
  "odroid-n2-libgl-wl"
)
provides=('odroid-c4-libgl' 'opencl-driver')
install=$pkgname.install
options=('!strip')
source=(
  "mali.conf"
  "license"
  "http://ppa.linuxfactory.or.kr/pool/non-free/m/mali-bifrost-driver/mali-bifrost-wayland-driver_0.1-5+202005160249~focal_arm64.deb"
)
md5sums=(
  '2ac761dc95cf5096d9313ad993dcf4be'
  '3da6d34563163f132815d9b8c5387a76'
  'e072997f63b0596025f5b928952a19eb'
)

prepare() {
  if [ -e "$srcdir/libs" ]; then
    rm -rf $srcdir/libs
  fi
}

build() {
  cd $srcdir
  mkdir $srcdir/libs

  # for some unknown reason this libgbm prevents linking issues
  # when compiling software.
  ar x mali-bifrost-wayland-driver_0.1-5+202005160249~focal_arm64.deb
  tar -xvJf data.tar.xz

  ln -sf libEGL.so.1               libs/libEGL.so
  ln -sf libEGL.so.1.1.0           libs/libEGL.so.1
  ln -sf libmali.so                libs/libEGL.so.1.1.0

  ln -sf libGLESv1_CM.so.1         libs/libGLESv1_CM.so
  ln -sf libmali.so                libs/libGLESv1_CM.so.1

  ln -sf libGLESv2.so.2            libs/libGLESv2.so
  ln -sf libGLESv2.so.2.1.0        libs/libGLESv2.so.2
  ln -sf libmali.so                libs/libGLESv2.so.2.1.0

  ln -sf libwayland-egl.so.1       libs/libwayland-egl.so
  ln -sf libwayland-egl.so.1.0.0   libs/libwayland-egl.so.1
  ln -sf libmali.so                libs/libwayland-egl.so.1.0.0

  ln -sf libOpenCL.so.1            libs/libOpenCL.so
  ln -sf libOpenCL.so.1.0.0        libs/libOpenCL.so.1
  ln -sf libmali.so                libs/libOpenCL.so.1.0.0
}

package() {
  install -d $pkgdir/etc/ld.so.conf.d
  install -d $pkgdir/usr/lib/mali-egl
  install -d $pkgdir/usr/share/doc/$pkgname

  cp mali.conf $pkgdir/etc/ld.so.conf.d/
  cp $srcdir/usr/lib/aarch64-linux-gnu/mali-g31/wayland/libmali.so $srcdir/libs/libmali.so
  chmod 0755 $srcdir/libs/libmali.so
  ln -sf /usr/lib/mali-egl/libmali.so $srcdir/libs/libmali.so.0
  cp -a $srcdir/libs/* $pkgdir/usr/lib/mali-egl/
  cp license $pkgdir/usr/share/doc/$pkgname/
}
