# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgname=odroid-gl4es
_gitname=gl4es
pkgver=2220.e34e97f
pkgrel=1
pkgdesc="OpenGL 2.1/1.5 to GL ES 2.0/1.1 translation library. (Odroid Build)"
arch=('aarch64')
url="https://github.com/ptitSeb/gl4es"
license=('MIT')
depends=('xorg-xrefresh')
makedepends=('git')
optdepends=(
  'odroid-n2-libgl-fb: hardware acceleration'
  'odroid-c4-libgl-fb: hardware acceleration'
)
source=(
  'git+https://github.com/ptitSeb/gl4es'
  'gl4es.conf'
  'gl4es.sh'
)
md5sums=(
  'SKIP'
  '50b819dc12b12c37c8ee66355720144e'
  'b1f7508c328628b662456d77798ffaba'
)

pkgver() {
  cd "${SRCDEST}/${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${srcdir}/${_gitname}"

  cmake -DODROID=1 -DCMAKE_BUILD_TYPE=RelWithDebInfo
  make
}

package() {
  install -d $pkgdir/etc/profile.d
  install -d $pkgdir/etc/ld.so.conf.d
  install -d $pkgdir/usr/lib/gl4es

  cp gl4es.conf $pkgdir/etc/ld.so.conf.d/
  cp gl4es.sh $pkgdir/etc/profile.d/

  cp $srcdir/$_gitname/lib/libGL.so.1 $pkgdir/usr/lib/gl4es/libGL.so.1.2.0
  ln -s /usr/lib/gl4es/libGL.so.1.2.0 $pkgdir/usr/lib/gl4es/libGL.so.1
  ln -s /usr/lib/gl4es/libGL.so.1.2.0 $pkgdir/usr/lib/gl4es/libGL.so
}
