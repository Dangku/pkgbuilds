# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgbase=mesa-arm-git
pkgname=('mesa-arm-git')
pkgdesc="An open-source implementation of the OpenGL specification"
pkgver=21.0.0_devel.131612.4cce4d22a72
pkgrel=1
arch=('aarch64')
makedepends=(
  'python-mako' 'libxml2' 'libx11' 'libdrm'
  'libxshmfence' 'libxxf86vm' 'libxdamage' 'libvdpau' 'libva'
  'wayland' 'wayland-protocols' 'elfutils' 'libomxil-bellagio'
  'clang' 'libglvnd' 'lm_sensors' 'libxrandr' 'meson'
)
depends=(
  'libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
  'libomxil-bellagio' 'libunwind' 'libglvnd' 'llvm' 'lm_sensors'
  'libclc' 'glslang' 'vulkan-icd-loader' 'wayland' 'zstd'
)
optdepends=(
  'opengl-man-pages: for the OpenGL API man pages'
  'mesa-vdpau: for accelerated video playback'
  'libva-mesa-driver: for accelerated video playback'
)
provides=('mesa' 'mesa-dri' 'mesa-libgl' 'opengl-driver')
conflicts=('mesa' 'mesa-dri' 'mesa-libgl')
replaces=('mesa-dri' 'mesa-libgl')
url="https://www.mesa3d.org/"
license=('custom')
source=(
  'mesa::git+https://gitlab.freedesktop.org/mesa/mesa.git'
  'LICENSE'
)
md5sums=(
  'SKIP'
  '1eff81373cde4b3d7b0ea7590431c962'
)

pkgver() {
  cd mesa
  read -r _ver <VERSION
  echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  meson setup mesa build \
    --buildtype=release \
    -D optimization=3 \
    -D strip=true \
    -D zstd=true \
    -D b_lto=true \
    -D prefix=/usr \
    -D sysconfdir=/etc \
    -D b_ndebug=true \
    -D debug=false \
    -D platforms=x11,wayland \
    -D dri-drivers= \
    -D gallium-drivers=panfrost,kmsro,swrast,virgl \
    -D vulkan-drivers= \
    -D dri3=true \
    -D egl=true \
    -D gallium-extra-hud=true \
    -D gallium-nine=false \
    -D gallium-omx=disabled \
    -D gallium-va=disabled \
    -D gallium-vdpau=disabled \
    -D gallium-xa=false \
    -D gallium-xvmc=false \
    -D gbm=true \
    -D gles1=false \
    -D gles2=true \
    -D glvnd=true \
    -D glx=dri \
    -D libunwind=disabled \
    -D llvm=true \
    -D lmsensors=true \
    -D osmesa=gallium \
    -D shared-glapi=true \
    -D valgrind=false

  # Print config
  meson configure build

  ninja $NINJAFLAGS -C build
}

package() {
  DESTDIR="$pkgdir" ninja $NINJAFLAGS -C build install

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 ${pkgdir}/usr/lib/libGLX_indirect.so.0

  install -Dt "$pkgdir"/usr/share/licenses/$pkgname "$srcdir"/LICENSE
}
